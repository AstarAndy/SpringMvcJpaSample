buildscript {
	ext {
		springBootVersion = '1.5.10.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

archivesBaseName = 'SpringMvcJpaSample'
group = 'com.astar.andy'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


ext {
	springCloudVersion = 'Edgware.SR2'
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

// This will pretty-up the tests
test {
	testLogging {
		// To also get out the spring output then include "standardOut" & standardError
		// To just get the name of the test and pass or fail then use the short version
		//events "passed", "skipped", "failed", "standardOut", "standardError"
		events "passed", "skipped", "failed"
	}
	afterSuite { desc, result ->
		if (!desc.parent) { // will match the outermost suite
			def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
			def startItem = '|  ', endItem = '  |'
			def repeatLength = startItem.length() + output.length() + endItem.length()
			println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
		}
	}
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')

	// Load JPA but exclude default tomcat junk
	compile('org.springframework.boot:spring-boot-starter-data-jpa') {
		exclude group: 'org.apache.tomcat', module: 'tomcat-jdbc'
	}

	compile('com.zaxxer:HikariCP')

	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-cloud-connectors')

	compile("org.springframework.boot:spring-boot-starter-undertow")
	compile('org.springframework.cloud:spring-cloud-starter')
	runtime('com.h2database:h2')

	compileOnly('org.projectlombok:lombok')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}
